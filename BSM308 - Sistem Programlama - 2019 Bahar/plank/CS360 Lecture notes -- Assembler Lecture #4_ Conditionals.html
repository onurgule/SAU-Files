
<!-- saved from url=(0085)http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/lecture.html -->
<html><head><meta http-equiv="Content-Type" content="text/html; charset=windows-1252"><title>CS360 Lecture notes -- Assembler Lecture #4: Conditionals</title>
</head><body bgcolor="ffffff">
<h2>CS360 Lecture notes -- Assembler Lecture #4: Conditionals</h2>
<ul>
<li><a href="http://web.eecs.utk.edu/~jplank">James S. Plank</a>
</li><li>Directory: <b>/home/plank/cs360/notes/Assembler4</b>
</li><li>Lecture notes:
    <a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/lecture.html">
    <b>
  http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/lecture.html
</b></a>
</li><li> Original notes: Sometime in the 1990's
</li><li> Latest Modification:
Wed Feb 21 17:04:46 EST 2018
</li></ul>
<hr>
Email that I have answered
<ul>
<li><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/mail.html">#1</a>
</li><li><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/mail2.html">#2</a>
</li></ul>
<hr>
This is the final lecture on assembler.  We'll go over branches,
recursion, and some other stuff.



<h1>Branch Instructions</h1>

Finally, there are "compare" and "branch" instructions which are
used to implement <b>if</b>, <b>for</b> and <b>while</b> constructs:
They work as follows:

<pre>   cmp %r0, %r1       This says to compare the values of the registers
                      r0 and r1, and set the control status register 
                      (<b>CSR</b>) to reflect the outcome.   The CSR
                      will store whether (r0==r1), (r0 &lt; r1) or (r0 &gt; r1).

   b l1               This says go (branch) directly to label l1.  This sets 
                      the pc to l1 rather than (pc+4).  Note that you
                      can't "return" from a branch like you can from a
                      "jsr" statement.

   beq l1             This says that if the CSR denotes that the two compared
                      values are equal, go (set the pc) to label l1.  
                      If the two compared values are not equal, the
                      next statement (pc+4) is executed.

   ble l1             These should be obvious (&lt;=, &lt;, &gt;=, &gt;, !=).             
   blt l1
   bge l1
   bgt l1
   bne l1
</pre>
                      
<hr>
Thus, conditional expressions such as <b>if</b>, <b>for</b> and <b>while</b>
statements are straightforward.

There are multiple ways to do them.  Here is how I recommend to do each 
type of statement:
<p>
<table border="3" cellpadding="5">
<tbody><tr><td valign="top">
<pre>if (<b>cond</b>) {
   <b>S1</b>
} else {
   <b>S2</b>
}
<b>S3</b>
</pre>
</td><td>
<pre>   <b>set up conditional</b>
   <b>branch on the <i>negation</i> of the conditional to</b> l1
   <b>S1</b>
   b l2
l1:
   <b>S2</b>
l2:
   <b>S3</b>
</pre>
</td></tr></tbody></table>
</p><p>
For example, here is a program that does a simple if-then-else: 


</p><p></p><center><table border="3" cellpadding="3"><tbody><tr><td valign="top">
<b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/cond1.c">cond1.c</a></b><pre>int a(int i, int j)
{
  int k;

  if (i &lt; j) {
    k = i;
  } else {
    k = j;
  }
  return k;
}

int main()
{
  return a(3, 4);
}
</pre></td><td valign="top">
<b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/cond1.jas">cond1.jas</a></b><pre>a:
  push #4                <font color="blue">/ Allocate k</font>
  
  ld [fp+12] -&gt; %r0      <font color="blue">/ Compare i &amp; j</font>
  ld [fp+16] -&gt; %r1      <font color="blue">/ Branch on negation of less-than</font>
  cmp %r0, %r1
  bge l1

  ld [fp+12] -&gt; %r0      <font color="blue">/ k = i</font>
  st %r0 -&gt; [fp]
  b l2

l1:
  ld [fp+16] -&gt; %r0      <font color="blue">/ k = j</font>
  st %r0 -&gt; [fp]

l2:
  ld [fp] -&gt; %r0         <font color="blue">/ return k</font>
  ret

main:
  mov #4 -&gt; %r0
  st %r0 -&gt; [sp]--
  mov #3 -&gt; %r0
  st %r0 -&gt; [sp]--
  jsr a
  ret
</pre></td></tr></tbody></table></center><p>
Trace through it with <b>jassem.tcl</b> to see that <b>r0</b> will be three when <b>main()</b>
returns.
</p><p>
</p><hr>
<table border="3" cellpadding="5">
<tbody><tr><td valign="top">
<pre>while (<b>cond</b>) {
   <b>S1</b>
} 
<b>S2</b>
</pre>
</td><td>
<pre>l1:
   <b>set up conditional</b>
   <b>branch on the <i>negation</i> of the conditional to</b> l2
   <b>S1</b>
   b l1
l2:
   <b>S2</b>
</pre>
</td></tr></tbody></table>
<p>
</p><hr>
<table border="3" cellpadding="5">
<tbody><tr><td valign="top">
<pre>for (<b>S1</b>; <b>cond</b>; <b>S2</b>) {
   <b>S3</b>
} 
<b>S4</b>
</pre>
</td><td>
<pre>   <b>S1</b>
   b l2
l1:
   <b>S2</b>
l2:
   <b>set up conditional</b>
   <b>branch on the <i>negation</i> of the conditional to</b> l3
   <b>S3</b>
   b l1
l3:
   <b>S4</b>
</pre>
</td></tr></tbody></table>
<hr>
For example, here's a program that uses a <b>for</b> loop to sum the numbers from 1 to <b>k</b>:

<p></p><center><table border="3" cellpadding="3"><tbody><tr><td valign="top">
<b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/cond2.c">cond2.c</a></b><pre>int a(int k)
{
  int i, j;

  j = 0;

  for (i = 1; i &lt;= k; i++) j += i;

  return j;
}

int main()
{
  int i;

  i = a(4);
}
</pre></td>
<td valign="top">
<b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/cond2.jas">cond2.jas</a></b><pre>a:
  push #8                      <font color="blue">/ Allocate i and j on the stack</font>
  
  st %g0 -&gt; [fp-4]             <font color="blue">/ Set j to zero</font>

  st %g1 -&gt; [fp]               <font color="blue">/ Initialize the for loop  (S1)</font>
  b l2

l1:

  ld [fp] -&gt; %r0               <font color="blue">/ Do i++ (S2)</font>
  add %r0, %g1 -&gt; %r0
  st %r0 -&gt; [fp]

l2:
  ld [fp] -&gt; %r0               <font color="blue">/ Perform the test, and </font>
  ld [fp+12] -&gt; %r1            <font color="blue">/ branch on the negation</font>
  cmp %r0, %r1
  bgt l3

  ld [fp-4] -&gt; %r0             <font color="blue">/ Do j += i  (S3)</font>
  ld [fp] -&gt; %r1
  add %r0, %r1 -&gt; %r0
  st %r0 -&gt; [fp-4]
  b l1

l3:                           
  ld [fp-4] -&gt; %r0             <font color="blue">/ return j (S4)</font>
  ret

main:
  push #4

  mov #4 -&gt; %r0
  st %r0 -&gt; [sp]--
  jsr a
  pop #4
  st %r0 -&gt; [fp]
  ret
</pre></td></tr></tbody></table></center><p>


</p><hr>
<h1>Recursion</h1>
<p>By now, recursive procedures shouldn't seem mysterious.  Below, I have a
recursive implementation of factorial:


</p><p></p><center><table border="3" cellpadding="3"><tbody><tr><td valign="top">
<b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact4.c">fact4.c</a></b><pre>int fact(int i)
{
  if (i == 0) return 1;
  return fact(i-1)*i;
}

int main()
{
  int i;

  i = fact(4);
}
</pre></td>
<td valign="top">
<b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact4.jas">fact4.jas</a></b><pre>fact:
    ld [fp+12] -&gt; %r0          <font color="blue">/ do the if statement</font>
    cmp %r0, %g0
    bne l1
    mov %g1 -&gt; %r0
    ret

l1:
    ld [fp+12] -&gt; %r0          <font color="blue">/ push i-1 on the stack</font>
    add %r0, %gm1 -&gt; %r0
    st %r0 -&gt; [sp]--
    jsr fact                   <font color="blue">/ recursive call to fact</font>
    pop #4                     <font color="blue">/ pop the argument off the stack</font>

    ld [fp+12] -&gt; %r1          <font color="blue">/ multiply fact(i-1)*i</font>
    mul %r0, %r1 -&gt; %r0
    ret

main: 
    push #4
    mov #4 -&gt; %r0
    st %r0 -&gt; [sp]--
    jsr fact
    pop #4
    st %r0 -&gt; [fp]
    ret
</pre></td></tr></tbody></table></center><p>

Each recursive call pushes a new stack frame.  You can use Use <b>jassem.tcl</b>
on the program <b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact4.c">fact4.c</a></b>, compiled into
<b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact4.jas">fact4.jas</a></b>,
to trace through <b>fact(4)</b>.
</p><p>
Additionally, I have two drawings that you can use for studying:
<a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact-unlabeled.png"><b>fact-unlabeled.png</b></a> (and
<a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact-unlabeled.pdf"><b>fact-unlabeled.pdf</b></a>) show a snapshot of the
stack at a certain point in the execution of <b>fact4.c</b>.
One potential test or homework question would be to label every byte on the stack
and say where we are in the program.  The answer is in
<a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact-labeled.png"><b>fact-labeled.png</b></a> (and
<a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact-labeled.pdf"><b>fact-labeled.pdf</b></a>).

</p><p>
</p><hr>
<h2>One More Example</h2>

I won't go over this in detail here, but behold 
<a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/bsort.c"><b>bsort.c</b></a>.  This is a simple bubble
sort of a 4-element array:
<pre>void bsort(int *a, int size)
{
  int i, j, tmp;

  for (i = size-1; i &gt; 0; i--) {
    for (j = 0; j &lt; i; j++) {
      if (a[j] &gt; a[j+1]) {
        tmp = a[j];
        a[j] = a[j+1];
        a[j+1] = tmp;
      }
    }
  }
}
      
main()
{
  int array[4];
  array[0] = 6;
  array[1] = 1;
  array[2] = 4;
  array[3] = 2;

  bsort(array, 4);
}
</pre>

There are a lot of array operations here, so the assembly
code is lengthy.  It is in <a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/bsort.jas"><b>bsort.jas</b></a>, 
and below:

<p>

</p><pre>bsort:
   push #12                 <font color="blue">/ i=fp-8, j=fp-4, tmp=fp</font>
   st %r2 -&gt; [sp]--         <font color="blue">/ Spill r2</font>
   
                            <font color="blue">/ For loop #1: labels f11, f12, f13</font>
   ld [fp+16] -&gt; %r0        <font color="blue">/ i = size-1</font>
   add %r0, %gm1 -&gt; %r0
   st %r0 -&gt; [fp-8]
   b f12

f11:
   ld [fp-8] -&gt; %r0         <font color="blue">/ i--</font>
   add %r0, %gm1 -&gt; %r0
   st %r0 -&gt; [fp-8]

f12:
   ld [fp-8] -&gt; %r0         <font color="blue">/ i &gt; 0</font>
   cmp %r0, %g0
   ble f13

                            <font color="blue">/ For loop #2: labels f21, f22, f23</font>
   st %g0 -&gt; [fp-4]         <font color="blue">/ j = 0</font>
   b f22

f21:
   ld [fp-4] -&gt; %r0         <font color="blue">/ j++</font>
   add %r0, %g1 -&gt; %r0
   st %r0 -&gt; [fp-4]

f22:
   ld [fp-4] -&gt; %r0
   ld [fp-8] -&gt; %r1
   cmp %r0, %r1
   bge f23

                            <font color="blue">/ If (a[j] &gt; a[j+1])</font>

   ld [fp-4] -&gt; %r0         <font color="blue">/ First put a[j] into register r0</font>
   mov #4 -&gt; %r1
   mul %r0, %r1 -&gt; %r0
   ld [fp+12] -&gt; %r1
   add %r0, %r1 -&gt; %r0
   ld [r0] -&gt; %r0

   ld [fp-4] -&gt; %r1         <font color="blue">/ Now put a[j+1] into register r1</font>
   add %r1, %g1 -&gt; %r1      <font color="blue">/ without touching r0</font>
   mov #4 -&gt; %r2           
   mul %r1, %r2 -&gt; %r1
   ld [fp+12] -&gt; %r2
   add %r1, %r2 -&gt; %r1
   ld [r1] -&gt; %r1

   cmp %r0, %r1
   ble i1

   ld [fp-4] -&gt; %r0         <font color="blue">/ tmp = a[j]</font>
   mov #4 -&gt; %r1
   mul %r0, %r1 -&gt; %r0
   ld [fp+12] -&gt; %r1
   add %r0, %r1 -&gt; %r0
   ld [r0] -&gt; %r0
   st %r0 -&gt; [fp]

   ld [fp-4] -&gt; %r0         <font color="blue">/ a[j] = a[j+1]</font>
   add %r0, %g1 -&gt; %r0      <font color="blue">/ Load a[j+1] into r0</font>
   mov #4 -&gt; %r1
   mul %r0, %r1 -&gt; %r0
   ld [fp+12] -&gt; %r1
   add %r0, %r1 -&gt; %r0
   ld [r0] -&gt; %r0
   ld [fp-4] -&gt; %r1         <font color="blue">/ Load &amp;(a[j]) into r1</font>
   mov #4 -&gt; %r2            
   mul %r1, %r2 -&gt; %r1
   ld [fp+12] -&gt; %r2
   add %r1, %r2 -&gt; %r1
   st %r0 -&gt; [r1]           <font color="blue">/ Store r0 into a[j]</font>

   ld [fp] -&gt; %r0           <font color="blue">/ a[j+1]  = tmp</font>
   ld [fp-4] -&gt; %r1        
   add %r1, %g1 -&gt; %r1    
   mov #4 -&gt; %r2            
   mul %r1, %r2 -&gt; %r1
   ld [fp+12] -&gt; %r2
   add %r1, %r2 -&gt; %r1
   st %r0 -&gt; [r1]

i1:                         <font color="blue">/ End of if statement</font>

   b f21                    <font color="blue">/ End of for loop #2  </font>
f23:

   b f11                    <font color="blue">/ End of for loop #1</font>
f13:
   ld ++[sp] -&gt; %r2
   ret

main:
   push #16

   mov #-1 -&gt; %r2       <font color="blue">/ This is just to show spilling</font>

   mov #6 -&gt; %r0
   st %r0 -&gt; [fp-12]
   mov #1 -&gt; %r0
   st %r0 -&gt; [fp-8]
   mov #4 -&gt; %r0
   st %r0 -&gt; [fp-4]
   mov #2 -&gt; %r0
   st %r0 -&gt; [fp]

   mov #4 -&gt; %r0
   st %r0 -&gt; [sp]--
   mov #12 -&gt; %r0
   sub %fp, %r0 -&gt; %r0
   st %r0 -&gt; [sp]--
   jsr bsort
   pop #8

   ret
</pre>

The execution of this with jassem is a bit cumbersome -- it goes blazingly
fast on my linux box, but not on my windows box -- this is not the
most efficient tcl/tk code in the world.  Oh well.  As always, make
sure you understand both the translation to assembly code, and 
the workings of the assembler.  Yes, this code is grossly inefficient
and can be made world's faster with the judicious use of some registers.


<hr>
<h1>Tail Recursion - Where Compiler Writers Bail Out Functional Programmers</h1>

<p>I will only go over this in class if there is time.  If not, only read this
if you are interested.    Since you are not supposed to optimize code, you are not
supposed to implement things like this on homeworks or exams.  
</p><p>
Suppose you write a procedure that is recursive, and has the feature that the
recursive call is returned.  For example, suppose you decided to write that
recursive factorial function as in 
<b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact5.c">fact5.c</a></b>:

</p><p></p><center><table border="3" cellpadding="3"><tbody><tr><td><pre>int fact(int i, int val_so_far)
{
  if (i == 0) return val_so_far;
  return fact(i-1, val_so_far * i);
}

int main()
{
  int i;

  i = fact(5, 1);
}
</pre></td></tr></tbody></table></center><p>

Since the recursive call is simply returned, there is a tricky compiler optimization called
"tail recursion."  What it does is the following:

</p><ul>
<li> Instead of pushing the arguments onto the stack, it changes them in place.  Here,
it would replace 
<b>val_so_far</b> with 
<b>(val_so_far * i)</b>, and then it would replace <b>i</b> with <b>(i-1)</b>.
<p>
</p></li><li> Instead of making a recursive <b>jsr</b> call, it makes an unconditional branch back
to <b>fact()</b>.
</li></ul>

The end result is that you reuse the stack frame for recursive calls, saving you time and
memory.  You get the recursion without the costs of recursion.  Here's
<b><a href="http://web.eecs.utk.edu/~jplank/plank/classes/cs360/360/notes/Assembler4/fact5.jas">fact5.jas</a></b>:

<p></p><center><table border="3" cellpadding="3"><tbody><tr><td><pre>fact:
  ld [fp+12] -&gt; %r0      <font color="blue">/ This is the base case.</font>
  cmp %r0, %g0           <font color="blue">/ If i == 0, return val_so_far</font>
  bne l1
  ld [fp+16] -&gt; %r0
  ret

l1:
  ld [fp+16] -&gt; %r0      <font color="blue">/ Replace val_so_far</font>
  ld [fp+12] -&gt; %r1      <font color="blue">/ with (val_so_far * i)</font>
  mul %r0, %r1 -&gt; %r0
  st %r0 -&gt; [fp+16]

  ld [fp+12] -&gt; %r0      <font color="blue">/ Replace i with i-1</font>
  add %r0, %gm1 -&gt; %r0
  st %r0 -&gt; [fp+12]

  b fact                 <font color="blue">/ Branch back instead of doing recursion.</font>

main:
  push #4
  st %g1 -&gt; [sp]--
  mov #5 -&gt; %r0
  st %r0 -&gt; [sp]--
  jsr fact
  pop #8
  st %r0 -&gt; [fp]
  ret
</pre></td></tr></tbody></table></center><p>

That's a neat optimization, isn't it?  It's one that functional programmers love, because
it allows them to write their tricky, modification-free functional programs and claim that
they can run as fast as normal programs.  

</p><hr>
<h1>Delay Slots</h1>

<p>I will only go over this in class if there is time.  If not, only read this
if you are interested.  

</p><p><b>In all assembler assignments in class, in homeworks and on tests,
assume that there is no delay slot.  This is just for your own knowledge.</b>

</p><p>Reading assembler from a random machine can be difficult, but usually
you can figure out how its assembler maps into the one defined in this
class.  One point of confusion which arose when our department had Sun Sparc 
processors is the <i>delay slot</i>.  There is a technique for speeding
up processors called "pipelining" which means that the CPU doesn't finish
executing the current instruction before it starts executing the next
instruction.  Usually, this does not involve much confusion.  However,
on <b>jsr</b> and <b>ret</b> and <b>b</b> instructions, there is a problem:
These instructions change the <b>pc</b>, which means that the next instruction
should not be executed.  But on a pipelined processor, by the time the
instruction is done, the next instruction has already been partially
executed.  

</p><p>The solution on Sparcs is that the instruction <i>after</i> 
the <b>jsr</b>,
<b>ret</b> and <b>b</b> is executed, and then control goes to the changed value
of the <b>pc</b>.  This instruction -- the one after the <b>jsr</b>,
<b>ret</b> or <b>b</b> -- is known as the <i>delay slot</i>.   Note that
the semantics of <b>jsr</b> must change too -- it must push <b>pc+8</b> onto
the stack so that when <b>ret</b> is called, it returns to the instruction
<i>after</i> the delay instruction.

</p><p>It is up to 
the compiler-writers to ensure that this slot is used correctly.  For example,
without compiler optimization, most compilers simply insert a <b>noop</b>
after the <b>jsr</b>, <b>ret</b> or <b>b</b>.  For example:
</p><pre>a(int i)
{
  return b(i+1)+1;
}
</pre>
compiles to:
<pre>a:
  ld [fp+12] -&gt; %r0            / Push i+1 onto the stack
  add %r0, %g1 -&gt; %r0
  st %r0 -&gt; [sp]--

  jsr b                        / Call procedure b
  noop                         / Delay slot
  pop #4

  add %r0, %g1 -&gt; %r0          / Put b(i+1)+1 into r0

  ret                          / return
  noop                         / delay slot
</pre>
An optimized compiler, however, will use the delay slot, which makes
code harder to read, since you have to remember that the instruction
after the <b>jsr</b>, <b>ret</b> or <b>b</b> gets executed.  Moreover,
subroutines return to the instruction <i>after</i> the instruction
after the <b>jsr</b> call.  Here's an example of the above procedure
compiled in such a way that the delay slots following the <b>jsr</b>
and <b>ret</b> statements are used.
<pre>a:
  ld [fp+12] -&gt; %r0            / Push i+1 onto the stack
  add %r0, %g1 -&gt; %r0

  jsr b                        / Call procedure b
  st %r0 -&gt; [sp]--
  pop #4

  ret                          / return
  add %r0, %g1 -&gt; %r0          / Put b(i+1)+1 into r0 -- this gets executed
                                 before the return actually occurs.
</pre>
</body></html>